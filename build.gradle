plugins {
    id "java"
}

group = "com.github.samlam140330"
version = "1.0.1"

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT"
    implementation "org.jetbrains:annotations:26.0.1"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "RestoreGamerule",
                "Specification-Vendor"    : "SamLam140330",
                "Specification-Version"   : "1",
                "Java-Version"            : JavaVersion.current(),
                "Gradle-Version"          : gradle.gradleVersion,
                "Implementation-Title"    : "RestoreGamerule",
                "Implementation-Vendor"   : "SamLam140330",
                "Implementation-Version"  : version,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
